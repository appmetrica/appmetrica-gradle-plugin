plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "io.appmetrica.analytics"
}

android {
    namespace "io.appmetrica.analytics.gradle.plugin.sample"
    buildToolsVersion versionBuildTools
    compileSdkVersion versionSdkTarget

    defaultConfig {
        applicationId "io.appmetrica.analytics.gradle.plugin.sample"
        minSdk 24
        targetSdkVersion versionSdkTarget
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {}
        snapshot {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    flavorDimensions 'first', 'second'
    productFlavors {
        firstOne {
            dimension "first"
        }
        firstTwo {
            dimension "first"
            appmetrica {
                enable = false
            }
        }
        secondOne {
            dimension "second"
        }
        secondTwo {
            dimension "second"
            appmetrica {
                enable = true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    ndkVersion "21.1.6352462"
    externalNativeBuild {
        cmake {
            path file("src/main/cpp/CMakeLists.txt")
            version "3.10.2"
        }
    }
}

// Priority: global > variant > buildType > flavor > default
appmetrica {
    // Global config: applies to all variants (highest priority)
    enable = true
    offline = true
    ndk {
        enable = project.provider { true }
    }

    // Per-variant config: overrides buildType and flavor settings
    variants {
        // Disable NDK for a specific variant
        register("firstTwoSecondOneSnapshot") {
            ndk {
                enable = false
            }
        }
    }
}

dependencies {
    implementation(project(":module"))
    implementation("io.appmetrica.analytics:analytics:6.0.0")
}
